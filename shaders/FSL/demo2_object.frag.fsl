#include "demo2_resources.h.fsl"

STRUCT(VSOutput)
{
    DATA(float4, Position, SV_Position);
    DATA(float4, WorldPos, Position);
    DATA(float4, Color,    COLOR);
    DATA(float4, Normal,   NORMAL);
};

float3 doDirectionalLight(
    float4 inColor, float4 inNormal, float4 worldPosition,
    float4 cameraPosition,
    float4 lightDirection, float4 lightColor,
    float diffuseIntensity, float ambientFactor, float specularStrength)
{
    float3 litColor = lightColor.rgb * inColor.rgb;

    float diffuseFactor = dot(normalize(inNormal.xyz), -normalize(lightDirection.xyz)) * diffuseIntensity;
    if(diffuseFactor < 0) diffuseFactor = 0;

    float3 viewDir = normalize(cameraPosition.xyz - worldPosition.xyz);
    float3 reflectDir = reflect(lightDirection.xyz, inNormal.xyz);

    float specularFactor = pow(max(dot(viewDir, reflectDir), 0.0), 32) * specularStrength * diffuseFactor;

    return litColor * (diffuseFactor + ambientFactor + specularFactor);
}

float4 PS_MAIN( VSOutput In )
{
    INIT_MAIN;
    float4 Out = {0.0, 0.0, 0.0, 0.0};

    int i = 0;
    for(i = 0; i < DIRECTIONAL_LIGHT_COUNT; i++)
    {
        Out.rgb += doDirectionalLight(
            In.Color, In.Normal, In.WorldPos, Get(cameraPosition),
            Get(directionalLightDirection[i]), Get(directionalLightColor[i]),
            Get(directionalIntensity[i]), Get(directionalAmbient[i]),
            Get(color).a);
    }

    Out.a = 1.0;

    RETURN(Out);
}